program main;
    -- Drei Missionare und drei Kannibalen wollen zusammen einen Fluß 
    -- überqueren. Das Boot hat nur Platz für zwei Passagiere.  Die Kannibalen
    -- sind hungrig, wenn die Missionare an einem Ufer in der Unterzahl sind, 
    -- haben sie ein Problem.  Die Aufgabe besteht darin, einen Fahrplan zu 
    -- erstellen, so dass hinterher alle am anderen Ufer ankommen und die
    -- Missionare nicht gefressen werden.
    
    -- Die einzelnen Punkte werden durch Tripel beschrieben.  Das Tripel
    -- [ M, K, B ] gibt an, dass M Missionare, K Kannibalen und B Boote am
    -- linken Ufer sind. Die Zahl der Missionare am rechten Ufer ist damit
    -- automatisch 3 - M, die Zahl der Kannibalen am rechten Ufer ist 3 - K
    -- und die Zahl der Boote am rechten Ufer ist 1 - B.

    -- P beschreibt alle möglichen Zustände bei denen die Missionare kein
    -- Problem haben.
    P := { [ M, K, B ] : M in {0..3}, K in {0..3}, B in {0,1}
                       | not problem(M, K) and not problem(3 - M, 3 - K) };
    print("#P = ", #P);

    print("Mögliche Zustände:\n");
    for [M, K, B] in P loop
        printState(M, K, B);
    end loop;
    print("\n");

    -- R1 beschreibt Zustandsübergänge, bei denen das Boot vom linken Ufer
    -- zum rechten Ufer fährt. PM ist die Anzahl der Missionare, die im Boot
    -- übersetzen und PK ist die Anzahl der Kannibalen im Boot.
    R1 := { [ [ M, K, 1 ], [ M - PM, K - PK, 0 ] ] : [ M, K, B ] in P, PM in {0..2}, PK in {0..2} |
                 PM + PK in {1,2} -- Das Boot darf nicht leer sein.
                                  -- Das Boot faßt maximal zwei Passagiere.
            and  PM <= M      -- Es können nicht mehr Missionare übersetzen, als Missionare am linken Ufer sind.
            and  PK <= K      -- Es können nicht mehr Kannibalen übersetzen, als Kannibalen am linken Ufer sind.
            and  [ M - PM, K - PK, 0 ] in P -- Der neue Zustand muss in der Menge aller möglichen Zustände liegen.
                                            -- Dadurch wird sichergestellt, dass es hinterher keine Probleme gibt.
          };  

    print("R1:\n");
    for [[M1, K1, B1], [M2, K2, B2]] in R1 loop
        if B1 /= 1 or B2 /= 0 then
            print("Fehler in R1: " + [[M1, K1, B1], [M2, K2, B2]]);
        end if;
        print();
        printState(M1, K1, B1);
        printBoat(M1, K1, B1, M2, K2, B2);
        printState(M2, K2, B2);
    end loop;
    print("#R1 = ", #R1);
    print("\n");

    -- R2 beschreibt Zustandsübergänge, bei denen das Boot vom rechten Ufer
    -- zum linken Ufer fährt. PM ist die Anzahl der Missionare, die im Boot
    -- übersetzen und PK ist die Anzahl der Kannibalen im Boot.
    R2 := { [ [ M, K, 0 ], [ M + PM, K + PK, 1 ] ] : [ M, K, B ] in P, PM in {0..2}, PK in {0..2} |
                 PM + PK in {1,2} -- Im Boot muss mindestens ein und höchstens zwei Passagiere sein.
            and  PM <= 3 - M  -- Es können nicht mehr Missionare übersetzen, als Missionare am rechten Ufer sind.
            and  PK <= 3 - K  -- Es können nicht mehr Kannibalen übersetzen, als Kannibalen am rechten Ufer sind.
            and  [ M + PM, K + PK, 1 ] in P -- Der neue Zustand muss erlaubt sein.
          };  

    print("R2:\n");
    for [[M1, K1, B1], [M2, K2, B2]] in R2 loop
        if B1 /= 0 or B2 /= 1 then
            print("Fehler in R2: " + [[M1, K1, B1], [M2, K2, B2]]);
        end if;
        print();
        printState(M1, K1, B1);
        printBoat(M1, K1, B1, M2, K2, B2);
        printState(M2, K2, B2);
    end loop;
    print("#R2 = ", #R2);
    print("\n");

    -- R ist die Menge aller Verbindungen zwischen Punkten.
    R := R1 + R2;

    print("R:\n");
    for [[M1, K1, B1], [M2, K2, B2]] in R loop
        print();
        printState(M1, K1, B1);
        printBoat(M1, K1, B1, M2, K2, B2);
        printState(M2, K2, B2);
    end loop;
    print("\n");
    print("#R = ", #R);


    -- Am Anfang sind alle links.
    start := [3,3,1];
    -- Am Ende sollen alle rechts sein, links ist dann niemand mehr.
    goal  := [0,0,0];
    -- Berechne alle möglichen Pfade.
    path  := reachable(start, goal, R);
    -- und geben diesen Schritt für Schritt aus.
    printPath(path);

    -- Es gibt ein Problem, wenn es an einem Ufer weniger Missionare als 
    -- sind, es sei denn, dass an dem Ufer überhaupt keine Missionare sind.
    procedure problem(M, K);
        return M > 0 and M < K;
    end problem;

    --------------------------------------------------------------------------
    --                                                                      --
    -- Die folgenden Prozeduren dienen nur dem Ausdrucken der Lösung.       --
    --                                                                      --
    --------------------------------------------------------------------------    

    -- DiesesPrädikat druckt die Lösung aus.
    procedure printPath(path);
        print("Lösung:\n");
        for i in {1 .. #path - 1} loop
            [ M1, K1, B1 ] := path(i);
            [ M2, K2, B2 ] := path(i+1);
            printState(M1, K1, B1);
            printBoat(M1, K1, B1, M2, K2, B2);
        end loop;
        [ M, K, B ] := path(#path);
        printState(M, K, B);
    end printPath;

    -- Die Prozedur printState(M, K, B) druckt den Zustand, bei dem
    -- M Missionare, K Kannibalen und B Boote am linken Ufer sind.
    procedure printState(M, K, B);
         print( fillCharsRight(M * "M", 6) + 
                fillCharsRight(K * "K", 6) + 
                fillCharsRight(B * "B", 3) + "    |~~~~~|    " + 
                fillCharsLeft((3 - M) * "M", 6) + 
                fillCharsLeft((3 - K) * "K", 6) + 
                fillCharsLeft((1 - B) * "B", 3) 
              );
    end printState;
    
    -- Die Prozedur printBoat(M1, K1, B1, M2, K2, B2) druckt den Zustand des
    -- Bootes, wenn der Zustand sich von [M1, K1, B1] zu [M2, K2, B2] ändert.
    procedure printBoat(M1, K1, B1, M2, K2, B2);
        if B1 = 1 then
            if M1 < M2 then
                print("Fehler in printBoat: negative Anzahl von Missionaren im Boot!");
            end if;
            if K1 < K2 then
                print("Fehler in printBoat: negative Anzahl von Kannibalen im Boot!");
            end if;
            print( 19 * " " + "> " + fillCharsBoth( (M1 - M2) * "M" + " " + (K1 - K2) * "K", 3 ) + " >" );
        else
            if M1 > M2 then
                print("Fehler in printBoat: negative Anzahl von Missionaren im Boot!");
            end if;
            if K1 > K2 then
                print("Fehler in printBoat: negative Anzahl von Kannibalen im Boot!");
            end if;
            print( 19 * " " + "< " + fillCharsBoth( (M2 - M1) * "M" + " " + (K2 - K1) * "K", 3 ) + " <" );
        end if;
    end printBoat;

    -- Die Prozedur fillCharsLeft(x, n) wandelt x in einen String der Länge n um.
    -- Dabei wird der String von links mit Leerzeichen aufgefüllt.
    procedure fillCharsLeft(x, n);
        s := str(x);
        m := n - #s;
        return m * " " + s;
    end fillCharsLeft;

    -- Die Prozedur fillCharsRight(x, n) wandelt x in einen String der Länge n um.
    -- Dabei wird der String von rechts mit Leerzeichen aufgefüllt.
    procedure fillCharsRight(x, n);
        s := str(x);
        m := n - #s;
        return s + m * " ";
    end fillCharsRight;

    -- Die Prozedur fillCharsBoth(x, n) wandelt x in einen String der Länge n um.
    -- Dabei wird der String von links und rechts mit Leerzeichen aufgefüllt.
    procedure fillCharsBoth(x, n);
        s := str(x);
        ml := (n     - #s) / 2;
        mr := (n + 1 - #s) / 2;
        return ml * " " + s + mr * " ";
    end fillCharsBoth;

    --------------------------------------------------------------------------
    --                                                                      --
    -- Die unten aufgeführten Prozeduren sind 1-zu-1 aus dem Programm zur   --
    -- Lösung des Wolf-Ziege-Kohl-Problems kopiert.                         --
    --                                                                      --
    --------------------------------------------------------------------------

    -- Check wether there is a path from x to y in R and compute it.
    procedure reachable(x, y, R);
        P := { [x] };
        loop
            Old_P := P;
            P     := R + path_product(P, P);
            Found := { p in P | p(1) = x and p(#p) = y };
            if Found /= {} then
                return arb Found;
            end if;
            if P = Old_P then
                return;
            end if;
        end loop;
    end reachable;

    -- The procedure call product(P, Q) computes the path product P * Q.
    procedure path_product(P, Q);
        return { add(p,q) : p in P, q in Q | p(#p) = q(1) and not cyclic(add(p,q)) };
    end path_product;    

    -- The procedure not_cyclic(p) checks whether the path p is cyclic.
    procedure cyclic(p);
        return #{ x : x in p } < #p;
    end cyclic;

    -- The product call add(p,q) computes the sum of the lists p and q.
    -- The last point of p has to be the first point of q.
    procedure add(p, q);
        return p + q(2..);
    end add;    

end main;

