program main;

    G := { [[1,2], 2], [[ 2,3], 1], [[1,3], 4], [[2,4], 7 ], [[4,5], 1], [[5, 1], 2] }; 
    T := closure(G);
    print(T);

    -- Gegeben Abstands-Relation D.
    -- Berechne Abstands-Funktion aus D
    procedure minimum(D);
        return { [ xy, min/ D{xy} ] : xy in domain(D) };
    end minimum;

    procedure closure(G);
        T := G;
        loop
            old_T := T;
            T := minimum(T + compose(G, T));
            if T = old_T then
                return T;
            end if;
        end loop;
    end closure;

    procedure compose(D1, D2);
        return { [[x, z], D1([x,y1]) + D2([y2,z]) ] : [x, y1] in domain(D1), [y2, z] in domain(D2) | y1 = y2 };
    end compose;

end main;

