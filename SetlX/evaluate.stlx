loadLibrary("termUtilities");
// Die Prozedur eval(f, I) wertet die aussagen-logische Formel f unter
// der aussagen-logischen Interpretation I aus.
evaluate := procedure(f, I) {
    match (f) {
        case true:              return true;
        case false:             return false;
        case p | isVariable(p): return I[varName(p)];
        case !g:                return !evaluate(g, I);
        case g && h:            return  evaluate(g, I) && evaluate(h, I);
        case g || h:            return  evaluate(g, I) || evaluate(h, I);
        case g => h:            return  evaluate(g, I) => evaluate(h, I);
        case g <==> h:          return  evaluate(g, I) == evaluate(h, I);
        default:                abort("syntax error in evaluate($f$, $I$)");
    }
};

f := parseTerm("(p => q) => (!p => q) => q");
I := { [ "p", true ], [ "q", true ] };
print( "evaluate($f$, $I$) = $evaluate(f, I)$" );
I := { [ "p", true ], [ "q", false ] };
print( "evaluate($f$, $I$) = $evaluate(f, I)$" );
I := { [ "p", false ], [ "q", true ] };
print( "evaluate($f$, $I$) = $evaluate(f, I)$" );
I := { [ "p", false ], [ "q", false ] };
print( "evaluate($f$, $I$) = $evaluate(f, I)$" );



   
