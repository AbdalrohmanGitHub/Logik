loadLibrary("termUtilities");
load("knf.stlx");
load("completeness.stlx");
createValuation := procedure(Literals) {
    result := {};
    for (l in Literals) {
        match(l) {
            case !p: result += { [p, false] };
            case  p: result += { [p, true ] };
        }
    }
    return result;
};
checkTautology := procedure(s) {
    print("\nChecking whether \"$s$\" is a tautology ...");
    Clauses  := normalize(parseTerm("!(" + s + ")"));
    Literals := findValuation(Clauses);
    if (Literals != false) {
        print("The formula $s$ is not a tautology. Counter example:");
        print(createValuation(Literals));
    } else {
        print("The formula $s$ is a tautology.");
    }
};
s := "p => p";
checkTautology(s);
s := "((p => q) => p) => p";
checkTautology(s);
s := "(p => q) => (q => p)";
checkTautology(s);
s := "p || (q && r) <==> (p || q) && (p || r)";
checkTautology(s);
s := "p && (q || r) <==> (p && q) || (p && r)";
checkTautology(s);
